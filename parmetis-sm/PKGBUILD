# Maintainer: Sigvald Marholm <marholm@marebakken.com>

# Adapted from the parmetis AUR package.
# Original contributor: George Eleftheriou <eleftg>
# Original contributor: Jed Brown <jed@59A2.org>
#
# Difference compared to original
# - The original does not work due to a small bug in build()

pkgname=parmetis-sm
pkgver=4.0.3.p6
_pkgver=4.0.3-p6
pkgrel=2
pkgdesc="A parallel graph partitioning library"
url="http://glaros.dtc.umn.edu/gkhome/metis/parmetis/overview"
arch=('x86_64')
license=('custom')
depends=('openmpi' 'metis')
makedepends=('cmake')
conflicts=(parmetis)
provides=(parmetis=4.0.3)
source=($pkgname-$pkgver.tar.gz::"https://bitbucket.org/petsc/pkg-parmetis/get/v${_pkgver}.tar.gz")
sha256sums=('663a20f45066d94a2732e8f38742047e89cd2b159cec7b3c6a7d834ae59d8b1f')

# "Upstream" is unmaintained and does not reply or apply to critical patches
#source=(http://glaros.dtc.umn.edu/gkhome/fetch/sw/parmetis/parmetis-$pkgver.tar.gz)

build() {
  cd "$srcdir"
  mv petsc-pkg-parmetis-* $pkgname-$pkgver
  cd "$pkgname-$pkgver"
  make config cc=mpicc mpicc=mpicc mpicxx=mpicxx shared=1 prefix=/usr
  make
}

package () {
  cd "$srcdir/$pkgname-$pkgver"
  make install "DESTDIR=$pkgdir"
}
